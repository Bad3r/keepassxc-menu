#!/bin/expect -f

# exp_internal 1
log_user 0
set timeout -1


proc defined_or_default {varname def} {
    upvar $varname var
    if {![info exists var]} {
        set var $def
    }
}

proc die {msg} {
    puts stderr $msg
    exit 1
}

set config $::env(HOME)/.config/keepassxc-dmenu/config.tcl
if {[file exists $config]} {
    source $config
} else {
    die "Provide config file at $config"
}

if {![info exists kp_database_path]} {
    die {Variable $kp_database_path must be defined}
} elseif {![file exists $kp_database_path]} {
    die "Database file not found at $kp_database_path"
}


defined_or_default kp_dmenu {rofi -dmenu -i}
defined_or_default kp_pinentry /usr/bin/pinentry-qt

set kp_appdir /tmp/keepassxc-dmenu/
file mkdir $kp_appdir
exec chmod 700 $kp_appdir
cd $kp_appdir


proc pinentry_get {} {
    spawn "$::kp_pinentry"
    expect "OK"
    send -- "SETDESC Enter Password for $::kp_database_path:\r"
    expect "OK"
    send -- "GETPIN\r"
    expect {
        "OK" { regexp -line {D (.*)\r} $expect_out(buffer) line pw }
        "cancelled" { die "User didn't input password, exiting." }
    }
    send -- "BYE\r"
    wait
    return $pw
}

proc enter_pw {kp_pw} {
    spawn keepassxc-cli open "$::kp_database_path"
    expect "Enter password"
    send -- "$kp_pw\r"
    expect "> "
    if {[regexp {Invalid credentials} $expect_out(buffer) match]} {
        die "Wrong Password"
    }
         
    send -- "ls -R -f\r"
    expect "> "

    set kp_accounts {}
    foreach line [lrange [split $expect_out(buffer) "\n"] 1 end] {
        set item [string trim $line]
        if {($item eq "") || ([string index $item end] in {">" "/"})} {
            continue
        }
        lappend kp_accounts $item
    }

    set window [exec xdotool getactivewindow]
    if {[catch {exec {*}$::kp_dmenu << [join $kp_accounts "\n"]} result] == 0} {
        send -- "show -s \"$result\"\r"
        expect "> "
        regexp -line {^Password: (.*)\r} $expect_out(buffer) line kp_entry_pw

        exec {*}{xdotool -} << "
             windowactivate --sync $window
             type --clearmodifiers -- $kp_entry_pw
        "        
    }
    
    send -- "quit\r"
    wait
}

proc readpipe {} {
    if {[gets $::pipe line] > 0} {
        if {![string eq $line exit]} {
            run $::kp_pw
        } else {
            cleanup
        }
    }
}

proc cleanup {} {
    try {
        close $::pipe
    } finally {
        file delete -force -- $::kp_appdir
        exit 1
    }
}

proc run {kp_pw} {
    try {
        enter_pw $kp_pw
    } on error {result option} {
        puts stderr $result
        cleanup
    }
}


### GET PASSWORD
set kp_pw [pinentry_get]
run $::kp_pw


### Go into background and listen on the FIFO
if {[fork] != 0} {
    exit
}

disconnect
trap {cleanup} {SIGTERM SIGINT}

set kp_pipe run
exec mkfifo $kp_pipe

set pipe [open "$kp_pipe" {RDWR NONBLOCK}]
fileevent $pipe readable readpipe

if {[info exists kp_timeout]} {
    set ::state waiting
    after [expr {60000 * $kp_timeout}] {set ::state timeout}
    vwait ::state
} else {
    vwait forever
}

cleanup
